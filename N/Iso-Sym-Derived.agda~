{-# OPTIONS  --safe #-}
{-# OPTIONS --termination-depth=2 #-}
open import Level using (0ℓ)

open import Relation.Binary using (Rel)
open import Relation.Binary.Definitions using (DecidableEquality)
open import Relation.Binary.Morphism.Definitions using (Homomorphic₂)
open import Relation.Binary.PropositionalEquality using (_≡_ ; _≢_ ; inspect ; setoid ; module ≡-Reasoning ; _≗_) renaming ([_] to [_]')
import Relation.Binary.Reasoning.Setoid as SR
import Relation.Binary.PropositionalEquality as Eq
open import Relation.Nullary.Decidable using (yes ; no)


open import Function using (_∘_ ; id)
open import Function.Definitions using (Injective)

open import Data.Product using (_×_ ; _,_ ; proj₁ ; proj₂ ; map₁ ; ∃ ; Σ ; Σ-syntax)
open import Data.Product.Relation.Binary.Pointwise.NonDependent as PW using (≡×≡⇒≡ ; Pointwise ; ≡⇒≡×≡)
open import Data.Nat hiding (_^_ ; _+_ ; _*_)
open import Agda.Builtin.Nat using (_-_)
import Data.Nat as Nat
open import Data.Bool hiding (_<_ ; _≤_)
open import Data.List hiding ([_] ; _++_ ; last ; head ; tail ; _∷ʳ_)
open import Data.Vec hiding ([_])
import Data.Vec as Vec
open import Data.Fin hiding (_+_ ; _-_)

open import Data.Maybe
open import Data.Sum using (_⊎_ ; inj₁ ; inj₂ ; [_,_] ; [_,_]′)
open import Data.Unit using (⊤ ; tt)
open import Data.Empty using (⊥ ; ⊥-elim)

open import Word.Base as WB hiding (wfoldl ; _*)
open import Word.Properties
import Presentation.Base as PB
import Presentation.Properties as PP
open PP using (NFProperty ; NFProperty')
import Presentation.CosetNF as CA
import Presentation.Reidemeister-Schreier as RS
module RSF = RS.Star-Injective-Full.Reidemeister-Schreier-Full

open import Presentation.Construct.Base hiding (_*_ ; _⊕_)
import Presentation.Construct.Properties.SemiDirectProduct2 as SDP2
import Presentation.Construct.Properties.DirectProduct as DP
import Presentation.Groups.Cyclic as Cyclic


open import Data.Fin using (Fin ; toℕ ; suc ; zero ; fromℕ)
open import Data.Fin.Properties using (suc-injective ; toℕ-inject₁ ; toℕ-fromℕ)
import Data.Nat.Properties as NP
open import Presentation.GroupLike
open import Presentation.Tactics hiding ([_])
open import Data.Nat.Primality



module N.Iso-Sym-Derived (p-2 : ℕ) (p-prime : Prime (2+ p-2))  where

pattern auto = Eq.refl

pattern ₀ = zero
pattern ₁ = suc ₀
pattern ₂ = suc ₁
pattern ₃ = suc ₂
pattern ₄ = 4
pattern ₅ = 5
pattern ₆ = 6
pattern ₇ = 7
pattern ₈ = 8
pattern ₉ = 9
pattern ₁₀ = 10
pattern ₁₁ = 11
pattern ₁₂ = 12
pattern ₁₃ = 13
pattern ₁₄ = 14
pattern ₁₅ = 15

pattern ₁₊ ⱼ = suc ⱼ
pattern ₂₊ ⱼ = suc (suc ⱼ)
pattern ₃₊ ⱼ = suc (suc (suc ⱼ))

private
  variable
    n : ℕ


open import Zp.ModularArithmetic
open PrimeModulus p-2 p-prime
open import N.Symplectic p-2 p-prime
open import N.Symplectic-Derived p-2 p-prime
open import N.Lemmas-2Qupit p-2 p-prime
open import N.NF1 p-2 p-prime
open Lemmas-2Q 2
open Symplectic-Derived-Gen
open Action
open Normal-Form1

module Sym  = Symplectic
module Sym'  = Lemmas-Sym
module SymDerived  = Symplectic-Derived-Gen
open Symplectic renaming (Gen to Gen₁ ; _QRel,_===_ to _QRel,_===₁_) using ()
open SymDerived renaming (Gen to Gen₂ ; _QRel,_===_ to _QRel,_===₂_) using ()
open Symplectic-GroupLike renaming (grouplike to grouplike₁) using ()
open Symplectic-Derived-GroupLike renaming (grouplike to grouplike₂) using ()


f : Sym.Gen n -> SymDerived.Gen n
f {₁₊ n} Sym.H-gen = SymDerived.H-gen ₁
f {₁₊ n} Sym.S-gen = SymDerived.S-gen ₁
f {₂₊ n} Sym.CZ-gen = SymDerived.CZ-gen ₁
f {₁₊ n} (x Sym.ₛ) = (f x) SymDerived.ₛ

g : SymDerived.Gen n -> Word (Sym.Gen n)
g {₁₊ n} (SymDerived.H-gen k) = Sym.H ^ toℕ k
g {₁₊ n} (SymDerived.S-gen k) = Sym.S ^ toℕ k
g {₂₊ n} (SymDerived.CZ-gen k) = Sym.CZ ^ toℕ k
g {₁₊ n} (x SymDerived.ₛ) = (g x) Sym.↑



-- open PB Sym._===_ renaming (_===_ to _===₁_ ; _≈_ to _≈₁_) using ()
-- open PB SymDerived._===_ renaming (_===_ to _===₂_ ; _≈_ to _≈₂_) using ()

-- open import Presentation.Morphism _===₁_ _===₂_
-- open GroupMorphs grouplike₁ grouplike₂


-- open PP Sym._===_ renaming (by-assoc-and to by-assoc-and₁ ; word-setoid to ws₁)
-- open PP SymDerived._===_ renaming (by-assoc-and to by-assoc-and₂ ; word-setoid to ws₂ ; by-assoc to by-assoc₂) using ()

-- open PB hiding (_===_)
-- open SymDerived hiding (p)

f* : Word (Gen₁ n) -> Word (Gen₂ n)
f* {n} = wmap (f {n})

f' : (Gen₁ n) -> Word (Gen₂ n)
f' = [_↑] ∘ f

f'* : Word (Gen₁ n) -> Word (Gen₂ n)
f'* = f' WB.*

open PB

lemma-f'*-^ : ∀ k w v ->
  let open PB ((₁₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) in
  f'* w ≈₂ v -> f'* (w ^ k) ≈₂ v ^ k
lemma-f'*-^ ₀ w v eq = refl
lemma-f'*-^ ₁ w v eq = eq
lemma-f'*-^ (₂₊ k) w v eq = cong eq (lemma-f'*-^ (₁₊ k) w v eq)

lemma-f'*-^↑ : ∀ k w v ->
  let open PB ((₂₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) in
  f'* (w Sym.↑) ≈₂ v ↑ -> f'* ((w ^ k) Sym.↑) ≈₂ (v ^ k) ↑
lemma-f'*-^↑ {n} ₀ w v eq = refl
lemma-f'*-^↑ {n} ₁ w v eq = eq
lemma-f'*-^↑ {n} (₂₊ k) w v eq = cong eq (lemma-f'*-^↑ {n} (₁₊ k) w v eq)

lemma-f'*-^↓ : ∀ k w v ->
  let open PB ((₂₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) in
  f'* (w Sym.↓) ≈₂ v ↓ -> f'* ((w ^ k) Sym.↓) ≈₂ (v ^ k) ↓
lemma-f'*-^↓ {n} ₀ w v eq = refl
lemma-f'*-^↓ {n} ₁ w v eq = eq
lemma-f'*-^↓ {n} (₂₊ k) w v eq = cong eq (lemma-f'*-^↓ {n} (₁₊ k) w v eq)

lemma-f* : ∀ k -> let open PB ((₁₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) in f* (Sym.S ^ k) ≈₂ SymDerived.S ^ k
lemma-f* ₀ = refl
lemma-f* ₁ = refl
lemma-f* (₂₊ k) = cong refl (lemma-f* (₁₊ k))

lemma-f*-CZ : ∀ k -> let open PB ((₂₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) in f* (Sym.CZ ^ k) ≈₂ SymDerived.CZ ^ k
lemma-f*-CZ ₀ = refl
lemma-f*-CZ ₁ = refl
lemma-f*-CZ (₂₊ k) = cong refl (lemma-f*-CZ (₁₊ k))

lemma-f'*-CZ : ∀ k -> let open PB ((₂₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) in f'* (Sym.CZ ^ k) ≈₂ SymDerived.CZ ^ k
lemma-f'*-CZ ₀ = refl
lemma-f'*-CZ ₁ = refl
lemma-f'*-CZ (₂₊ k) = cong refl (lemma-f'*-CZ (₁₊ k))


lemma-f'* : ∀ k -> let open PB ((₁₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) in f'* (Sym.S ^ k) ≈₂ SymDerived.S ^ k
lemma-f'* ₀ = refl
lemma-f'* ₁ = refl
lemma-f'* (₂₊ k) = cong refl (lemma-f'* (₁₊ k))


lemma-f'*-H : ∀ k -> let open PB ((₁₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) in f'* (Sym.H ^ k) ≈₂ SymDerived.H ^ k
lemma-f'*-H ₀ = refl
lemma-f'*-H ₁ = refl
lemma-f'*-H (₂₊ k) = cong refl (lemma-f'*-H (₁₊ k))


lemma-f'*-M : ∀ x -> let open PB ((₁₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) in f'* (Sym.M x) ≈₂ SymDerived.M x
lemma-f'*-M {n} x' = begin
  f'* (Sym.M x') ≈⟨ refl ⟩
  f'* (Sym.S^ x • Sym.H • Sym.S^ x⁻¹ • Sym.H • Sym.S^ x • Sym.H) ≈⟨ refl ⟩
  f'* (Sym.S^ x) • f'* Sym.H • f'* (Sym.S^ x⁻¹) • f'* Sym.H • f'* (Sym.S^ x) • f'* (Sym.H) ≈⟨ cong (lemma-f'* (toℕ x)) (cong refl (cong (lemma-f'* (toℕ x⁻¹)) (cong refl (cong (lemma-f'* (toℕ x)) refl)))) ⟩
  (S ^ toℕ x) • H • (S ^ toℕ x⁻¹) • H • (S ^ toℕ x) • (H) ≈⟨ cong (sym (axiom (derived-S x))) (cong refl (cong (sym (axiom (derived-S x⁻¹))) (cong refl (sym (cong (axiom (derived-S x)) refl))))) ⟩
  (S^ x) • H • (S^ x⁻¹) • H • (S^ x) • (H) ≈⟨ refl ⟩
  M x' ∎
  where
  open PB ((₁₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) using ()
  open PP ((₁₊ n) QRel,_===₂_)
  open SR word-setoid
  x = x' .proj₁
  x⁻¹ = ((x' ⁻¹) .proj₁ )

lemma-f'*-↑ : ∀ w -> let open PB ((₁₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) in f'* {₁₊ n} (w Sym.↑) ≈₂ f'* w ↑
lemma-f'*-↑ {n} [ x ↑] = refl
lemma-f'*-↑ {n} ε = refl
lemma-f'*-↑ {n} (w • w₁) = cong (lemma-f'*-↑ w) (lemma-f'*-↑ w₁)

lemma-f'*-M↑ : ∀ x -> let open PB ((₂₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) in f'* {₂₊ n} (Sym.M x Sym.↑) ≈₂ SymDerived.M x ↑
lemma-f'*-M↑ {n} x' = begin
  f'* (Sym.M x' Sym.↑) ≈⟨ lemma-f'*-↑ (Sym.M x') ⟩
  (f'* (Sym.M x')) ↑ ≈⟨ lemma-cong↑ _ _ (lemma-f'*-M x') ⟩
  M x' ↑ ≈⟨ _≈₂_.refl ⟩
  SymDerived.M x' ↑ ∎
  where
  open PB ((₂₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) using ()
  open PP ((₂₊ n) QRel,_===₂_)
  open SR word-setoid
  x = x' .proj₁
  x⁻¹ = ((x' ⁻¹) .proj₁ )

f-well-defined :
  let open PB ((n) QRel,_===₁_) renaming (_===_ to _===₁_) in
  let open PB ((n) QRel,_===₂_) renaming (_≈_ to _≈₂_) in
  ∀ {w v} -> w ===₁ v -> f'* w ≈₂ f'* v
f-well-defined {₁₊ n} Symplectic.order-S = begin
  f'* (Sym.S • Sym.S ^ ₁₊ p-2) ≡⟨ lemma-* ([ Sym.S-gen ↑] • Sym.S ^ ₁₊ p-2) ⟩
  (wmap f) (Sym.S • Sym.S ^ ₁₊ p-2) ≈⟨ lemma-f* (₂₊ p-2) ⟩
  SymDerived.S ^ p ≈⟨ axiom order-S ⟩
  f'* ε ∎
  where
  open PB ((₁₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) using ()
  open PP ((₁₊ n) QRel,_===₂_)
  open SR word-setoid

f-well-defined {n} Symplectic.order-H = axiom order-H
f-well-defined {n} Symplectic.order-SH = axiom order-SH
f-well-defined {n} Symplectic.comm-HHS = axiom comm-HHS
f-well-defined {₁₊ n} (Symplectic.M-mul x y) = begin
  f'* (Sym.M x • Sym.M y) ≈⟨ refl ⟩
  f'* (Sym.M x) • f'* (Sym.M y) ≈⟨ cong (lemma-f'*-M x) (lemma-f'*-M y) ⟩
  (SymDerived.M x) • (SymDerived.M y) ≈⟨ axiom (M-mul x y) ⟩
  (SymDerived.M (x *' y)) ≈⟨ sym (lemma-f'*-M (x *' y)) ⟩
  f'* (Sym.M (x *' y)) ∎
  where
  open PB ((₁₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) using ()
  open PP ((₁₊ n) QRel,_===₂_)
  open SR word-setoid
f-well-defined {₁₊ n} (Sym.semi-MS (x , nz)) = begin
  f'* (Sym.M (x , nz) • Sym.S) ≈⟨ cong (lemma-f'*-M (x , nz)) (lemma-f'* ₁) ⟩
  (M (x , nz) • S) ≈⟨ axiom (semi-MS (x , nz)) ⟩
  (S^ (x * x) • M (x , nz)) ≈⟨ cong (axiom (derived-S (x * x))) refl ⟩
  (S ^ toℕ (x * x) • M (x , nz)) ≈⟨ cong (sym (lemma-f'* (toℕ (x * x)))) (sym (lemma-f'*-M (x , nz))) ⟩
  f'* (Sym.S^ (x * x) • Sym.M (x , nz)) ∎
  where
  open PB ((₁₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) using ()
  open PP ((₁₊ n) QRel,_===₂_)
  open SR word-setoid

f-well-defined {₂₊ n} (Sym.semi-M↑CZ x) = begin
  f'* ((Sym.M x Sym.↑) • Sym.CZ) ≡⟨ Eq.cong₂ _•_ Eq.refl (lemma-* {f = f} Sym.CZ) ⟩
  f'* ((Sym.M x Sym.↑)) • (wmap f Sym.CZ) ≈⟨ cong (lemma-f'*-M↑  x) (lemma-f*-CZ ₁) ⟩
  (SymDerived.M x SymDerived.↑) • (SymDerived.CZ) ≈⟨ _≈₂_.axiom (_QRel,_===₂_.semi-M↑CZ x) ⟩
  (SymDerived.CZ^ (x SymDerived.^1)) • (SymDerived.M x SymDerived.↑) ≈⟨ sym (cong (_≈₂_.trans (lemma-f*-CZ (toℕ (x .proj₁)))
                                                                                    (_≈₂_.sym
                                                                                     (_≈₂_.axiom (_QRel,_===₂_.derived-CZ (x SymDerived.^1))))) (lemma-f'*-M↑ x)) ⟩
  (wmap f) (Sym.CZ^ (x Sym.^1)) • f'* ((Sym.M x Sym.↑)) ≡⟨ Eq.sym (Eq.cong₂ _•_  (lemma-* {f = f} (Sym.CZ^ (x Sym.^1))) auto) ⟩
  f'* (Sym.CZ^ (x Sym.^1) • Sym.M x Sym.↑) ≈⟨ _≈₂_.refl ⟩
  (f'* (Sym.CZ^ (x Sym.^1) • (Sym.M x Sym.↑))) ∎
  where
  open PB ((₂₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) using ()
  open PP ((₂₊ n) QRel,_===₂_)
  open SR word-setoid
  
f-well-defined {₂₊ n} (Sym.semi-M↓CZ x) = begin
  f'* ((Sym.M x Sym.↓) • Sym.CZ) ≡⟨ Eq.cong₂ _•_ Eq.refl (lemma-* {f = f} Sym.CZ) ⟩
  f'* ((Sym.M x Sym.↓)) • (wmap f Sym.CZ) ≈⟨ cong (lemma-f'*-M x) (lemma-f*-CZ ₁) ⟩
  (SymDerived.M x SymDerived.↓) • (SymDerived.CZ) ≈⟨ _≈₂_.axiom (_QRel,_===₂_.semi-M↓CZ x) ⟩
  (SymDerived.CZ^ (x SymDerived.^1)) • (SymDerived.M x SymDerived.↓) ≈⟨ sym (cong (_≈₂_.trans (lemma-f*-CZ (toℕ (x .proj₁)))
                                                                                    (_≈₂_.sym
                                                                                     (_≈₂_.axiom (_QRel,_===₂_.derived-CZ (x SymDerived.^1))))) (lemma-f'*-M x)) ⟩
  (wmap f) (Sym.CZ^ (x Sym.^1)) • f'* ((Sym.M x Sym.↓)) ≡⟨ Eq.sym (Eq.cong₂ _•_  (lemma-* {f = f} (Sym.CZ^ (x Sym.^1))) auto) ⟩
  f'* (Sym.CZ^ (x Sym.^1) • Sym.M x Sym.↓) ≈⟨ _≈₂_.refl ⟩
  (f'* (Sym.CZ^ (x Sym.^1) • (Sym.M x Sym.↓))) ∎
  where
  open PB ((₂₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) using ()
  open PP ((₂₊ n) QRel,_===₂_)
  open SR word-setoid

f-well-defined {₂₊ n} Sym.order-CZ = begin
  f'* (Sym.CZ • Sym.CZ ^ ₁₊ p-2) ≡⟨ lemma-* ([ Sym.CZ-gen ↑] • Sym.CZ ^ ₁₊ p-2) ⟩
  (wmap f) (Sym.CZ • Sym.CZ ^ ₁₊ p-2) ≈⟨ lemma-f*-CZ (₂₊ p-2) ⟩
  SymDerived.CZ ^ p ≈⟨ axiom order-CZ ⟩
  f'* ε ∎
  where
  open PB ((₂₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) using ()
  open PP ((₂₊ n) QRel,_===₂_)
  open SR word-setoid
  
f-well-defined Sym.comm-CZ-S↓ = axiom _QRel,_===₂_.comm-CZ-S↓
f-well-defined Sym.comm-CZ-S↑ = axiom _QRel,_===₂_.comm-CZ-S↑
f-well-defined {₂₊ n} Sym.selinger-c10 = begin
  f'* (Sym.CZ • (Sym.H Sym.↑) • Sym.CZ) ≈⟨ _≈₂_.refl ⟩
  (CZ • (H ↑) • CZ) ≈⟨ _≈₂_.axiom _QRel,_===₂_.selinger-c10 ⟩
  (((S⁻¹ ↑) • (H ↑) • (S⁻¹ ↑) •  CZ • (H ↑) • (S⁻¹ ↑) • (S⁻¹ ↓))) ≈⟨ sym (cong (lemma-f'*-^↑ p-1 Sym.S SymDerived.S _≈₂_.refl) (cong _≈₂_.refl (cong (lemma-f'*-^↑ p-1 Sym.S SymDerived.S _≈₂_.refl) (cong _≈₂_.refl (cong _≈₂_.refl (cong (lemma-f'*-^↑ p-1 Sym.S SymDerived.S _≈₂_.refl) (lemma-f'*-^↓ p-1 Sym.S SymDerived.S _≈₂_.refl))))))) ⟩
  (f'* ((Sym.S⁻¹ Sym.↑) • (Sym.H Sym.↑) • (Sym.S⁻¹ Sym.↑) •  Sym.CZ • (Sym.H Sym.↑) • (Sym.S⁻¹ Sym.↑) • (Sym.S⁻¹ Sym.↓))) ∎
  where
  open PB ((₂₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) using ()
  open PP ((₂₊ n) QRel,_===₂_)
  open SR word-setoid
f-well-defined {₂₊ n} Sym.selinger-c11 = begin
  f'* (Sym.CZ • (Sym.H Sym.↓) • Sym.CZ) ≈⟨ _≈₂_.refl ⟩
  (CZ • (H ↓) • CZ) ≈⟨ _≈₂_.axiom _QRel,_===₂_.selinger-c11 ⟩
  (((S⁻¹ ↓) • (H ↓) • (S⁻¹ ↓) •  CZ • (H ↓) • (S⁻¹ ↓) • (S⁻¹ ↑))) ≈⟨ sym (cong (lemma-f'*-^↓ p-1 Sym.S SymDerived.S _≈₂_.refl) (cong _≈₂_.refl (cong (lemma-f'*-^↓ p-1 Sym.S SymDerived.S _≈₂_.refl) (cong _≈₂_.refl (cong _≈₂_.refl (cong (lemma-f'*-^↓ p-1 Sym.S SymDerived.S _≈₂_.refl) (lemma-f'*-^↑ p-1 Sym.S SymDerived.S _≈₂_.refl))))))) ⟩
  (f'* ((Sym.S⁻¹ Sym.↓) • (Sym.H Sym.↓) • (Sym.S⁻¹ Sym.↓) •  Sym.CZ • (Sym.H Sym.↓) • (Sym.S⁻¹ Sym.↓) • (Sym.S⁻¹ Sym.↑))) ∎
  where
  open PB ((₂₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) using ()
  open PP ((₂₊ n) QRel,_===₂_)
  open SR word-setoid
f-well-defined Sym.selinger-c12 = axiom _QRel,_===₂_.selinger-c12
f-well-defined Sym.selinger-c13 = axiom _QRel,_===₂_.selinger-c13
f-well-defined Sym.selinger-c14 = axiom _QRel,_===₂_.selinger-c14
f-well-defined Sym.selinger-c15 = axiom _QRel,_===₂_.selinger-c15
f-well-defined Sym.comm-H = axiom _QRel,_===₂_.comm-H
f-well-defined Sym.comm-S = axiom _QRel,_===₂_.comm-S
f-well-defined Sym.comm-CZ = axiom _QRel,_===₂_.comm-CZ
f-well-defined {₂₊ n} (Sym.cong↑ {w = w} {v = v} x) = begin
  f'* (w Sym.↑) ≈⟨ lemma-f'*-↑ w ⟩
  f'* w ↑ ≈⟨ lemma-cong↑ _ _ (f-well-defined x) ⟩
  f'* v ↑ ≈⟨ sym (lemma-f'*-↑ v) ⟩
  (f'* (v Sym.↑)) ∎
  where
  open PB ((₂₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) using ()
  open PP ((₂₊ n) QRel,_===₂_)
  open SR word-setoid


g* : Word (Gen₂ n) -> Word (Gen₁ n)
g* {n} = g WB.*

lemma-g* : let open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_) in
  ∀ k -> g* (S ^ k) ≈₁ Sym.S ^ k
lemma-g* ₀ = refl
lemma-g* ₁ = refl
lemma-g* (₂₊ k) = cong refl (lemma-g* (₁₊ k))

lemma-g*-CZ : let open PB ((₂₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_) in
  ∀ k -> g* (CZ ^ k) ≈₁ Sym.CZ ^ k
lemma-g*-CZ ₀ = refl
lemma-g*-CZ ₁ = refl
lemma-g*-CZ (₂₊ k) = cong refl (lemma-g*-CZ (₁₊ k))


lemma-g*-H : let open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_) in ∀ k -> g* (H ^ k) ≈₁ Sym.H ^ k
lemma-g*-H ₀ = refl
lemma-g*-H ₁ = refl
lemma-g*-H (₂₊ k) = cong refl (lemma-g*-H (₁₊ k))

lemma-g*'-H : let open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_) in ∀ k -> g* (H^ k) ≈₁ Sym.H ^ toℕ k
lemma-g*'-H ₀ = refl
lemma-g*'-H ₁ = refl
lemma-g*'-H ₂ = refl
lemma-g*'-H ₃ = refl

lemma-g*-↑ : ∀ w -> let open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_) in g* {₁₊ n} (w SymDerived.↑) ≈₁ g* w Sym.↑
lemma-g*-↑ {n} [ x ↑] = refl
lemma-g*-↑ {n} ε = refl
lemma-g*-↑ {n} (w • w₁) = cong (lemma-g*-↑ w) (lemma-g*-↑ w₁)


lemma-g*-M : let open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_) in ∀ x -> g* (M x) ≈₁ Sym.M x
lemma-g*-M {n} x' = begin
  g* (M x') ≈⟨ refl₁ ⟩
  g* (S^ x • H • S^ x⁻¹ • H • S^ x • H) ≈⟨ refl₁ ⟩
  g* (S^ x) • g* H • g* (S^ x⁻¹) • g* H • g* (S^ x) • g* (H) ≈⟨ cong₁ (sym₁ ( lemma-g* (toℕ x))) (cong₁ refl₁ (cong₁ (sym₁ ( lemma-g* (toℕ x⁻¹))) (cong₁ refl₁ (sym₁ (cong₁ ( lemma-g* (toℕ x)) refl₁))))) ⟩
  g* (S ^ toℕ x) • g* H • g* (S ^ toℕ x⁻¹) • g* H • g* (S ^ toℕ x) • g* (H) ≈⟨ cong₁ (lemma-g* (toℕ x)) (cong₁ refl₁ (cong₁ (lemma-g* (toℕ x⁻¹)) (cong₁ refl₁ (cong₁ (lemma-g* (toℕ x)) refl₁)))) ⟩
  (Sym.S ^ toℕ x) • Sym.H • (Sym.S ^ toℕ x⁻¹) • Sym.H • (Sym.S ^ toℕ x) • (Sym.H) ≈⟨ refl₁ ⟩
  (Sym.S^ x) • Sym.H • (Sym.S^ x⁻¹) • Sym.H • (Sym.S^ x) • (Sym.H) ≈⟨ refl₁ ⟩
  Sym.M x' ∎
  where
  open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_ ; refl to refl₁ ; cong to cong₁ ; sym to sym₁)
  open PP ((₁₊ n) QRel,_===₁_)
  open SR word-setoid
  x = x' .proj₁
  x⁻¹ = ((x' ⁻¹) .proj₁ )


g-well-defined :
  let open PB (( n) QRel,_===₁_) renaming (_≈_ to _≈₁_) in
  let open PB ((n) QRel,_===₂_) renaming (_≈_ to _≈₂_ ; _===_ to _===₂_) in
  ∀ {w v} -> w ===₂ v -> g* w ≈₁ g* v
g-well-defined {₁₊ n} order-S = begin
  g* (S • S ^ ₁₊ p-2) ≈⟨ lemma-g* p ⟩
  (Sym.S ^ p) ≈⟨ axiom₁ Sym.order-S ⟩
  g* ε ∎
  where
  open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_ ; refl to refl₁ ; cong to cong₁ ; sym to sym₁ ; axiom to axiom₁)
  open PP ((₁₊ n) QRel,_===₁_)
  open SR word-setoid

g-well-defined order-H = axiom Sym.order-H
g-well-defined order-SH = axiom Sym.order-SH
g-well-defined comm-HHS = axiom Sym.comm-HHS

g-well-defined {₁₊ n} (M-mul x y) = begin
  g* (M x • M y) ≈⟨ refl ⟩
  g* (M x) • g* (M y) ≈⟨ cong (lemma-g*-M x) (lemma-g*-M y) ⟩
  (Sym.M x) • (Sym.M y) ≈⟨ axiom (Sym.M-mul x y) ⟩
  (Sym.M (x *' y)) ≈⟨ sym (lemma-g*-M (x *' y)) ⟩
  g* (M (x *' y)) ∎
  where
  open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_ ; refl to refl₁ ; cong to cong₁ ; sym to sym₁ ; axiom to axiom₁)
  open PP ((₁₊ n) QRel,_===₁_)
  open SR word-setoid


g-well-defined {₁₊ n} (semi-MS (x , nz)) = begin
  g* (M (x , nz) • S) ≈⟨ cong (lemma-g*-M (x , nz)) (lemma-g* ₁) ⟩
  (Sym.M (x , nz) • Sym.S) ≈⟨ axiom (Sym.semi-MS (x , nz)) ⟩
  (Sym.S^ (x * x) • Sym.M (x , nz)) ≈⟨ cong (refl) refl ⟩
  (Sym.S ^ toℕ (x * x) • Sym.M (x , nz)) ≈⟨ cong (sym (lemma-g* (toℕ (x * x)))) (sym (lemma-g*-M (x , nz))) ⟩
  g* (S ^ toℕ (x * x) • M (x , nz)) ≈⟨ cong (sym (g-well-defined (derived-S (fromℕ< _)))) refl ⟩
  g* (S^ (x * x) • M (x , nz)) ∎
  where
  open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_ ; refl to refl₁ ; cong to cong₁ ; sym to sym₁ ; axiom to axiom₁)
  open PP ((₁₊ n) QRel,_===₁_)
  open SR word-setoid

g-well-defined {₁₊ n} (derived-S k) = begin
  g* [ S-gen k ↑] ≈⟨ refl ⟩
  Sym.S ^ toℕ k ≈⟨ sym (lemma-g* (toℕ k)) ⟩
  g* (S ^ toℕ k) ∎
  where
  open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_ ; refl to refl₁ ; cong to cong₁ ; sym to sym₁ ; axiom to axiom₁)
  open PP ((₁₊ n) QRel,_===₁_)
  open SR word-setoid


g-well-defined {₁₊ n} (derived-H k) = begin
  g* [ H-gen k ↑] ≈⟨ lemma-g*'-H k ⟩
  Sym.H ^ toℕ k ≈⟨ sym (lemma-g*-H (toℕ k)) ⟩
  g* (H ^ toℕ k) ∎
  where
  open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_ ; refl to refl₁ ; cong to cong₁ ; sym to sym₁ ; axiom to axiom₁)
  open PP ((₁₊ n) QRel,_===₁_)
  open SR word-setoid



g-well-defined {₁₊ n} (semi-M↑CZ x) = begin
  g* ((SymDerived.M x SymDerived.↑) • SymDerived.CZ) ≈⟨ cong refl₁ refl₁ ⟩
  ((Sym.M x Sym.↑) • Sym.CZ) ≈⟨ axiom₁ (_QRel,_===₁_.semi-M↑CZ x) ⟩
  Sym.CZ^ (x Sym.^1) • (Sym.M x Sym.↑)  ≈⟨ refl₁ ⟩
  g* (SymDerived.CZ^ (x SymDerived.^1) • (SymDerived.M x SymDerived.↑)) ∎
  where
  open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_ ; refl to refl₁ ; cong to cong₁ ; sym to sym₁ ; axiom to axiom₁)
  open PP ((₁₊ n) QRel,_===₁_)
  open SR word-setoid
g-well-defined {₁₊ n} (semi-M↓CZ x) = begin
  g* ((SymDerived.M x SymDerived.↓) • SymDerived.CZ) ≈⟨ cong (refl) refl₁ ⟩
  ((Sym.M x Sym.↓) • Sym.CZ) ≈⟨ axiom₁ (_QRel,_===₁_.semi-M↓CZ x) ⟩
  Sym.CZ^ (x Sym.^1) • (Sym.M x Sym.↓)  ≈⟨ cong refl₁ (sym (refl)) ⟩
  g* (SymDerived.CZ^ (x SymDerived.^1) • (SymDerived.M x SymDerived.↓)) ∎
  where
  open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_ ; refl to refl₁ ; cong to cong₁ ; sym to sym₁ ; axiom to axiom₁)
  open PP ((₁₊ n) QRel,_===₁_)
  open SR word-setoid
g-well-defined {₁₊ n} order-CZ = begin
  g* (CZ • CZ ^ ₁₊ p-2) ≈⟨ lemma-g*-CZ p ⟩
  (Sym.CZ ^ p) ≈⟨ axiom₁ Sym.order-CZ ⟩
  g* ε ∎
  where
  open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_ ; refl to refl₁ ; cong to cong₁ ; sym to sym₁ ; axiom to axiom₁)
  open PP ((₁₊ n) QRel,_===₁_)
  open SR word-setoid
g-well-defined {n} comm-CZ-S↓ = axiom _QRel,_===₁_.comm-CZ-S↓
g-well-defined {n} comm-CZ-S↑ = axiom _QRel,_===₁_.comm-CZ-S↑
g-well-defined {₁₊ n} selinger-c10 = begin
  g* (CZ • H ↑ • CZ) ≈⟨ refl₁ ⟩
  (Sym.CZ • Sym.H Sym.↑ • Sym.CZ) ≈⟨ axiom₁ _QRel,_===₁_.selinger-c10 ⟩
  (Sym.S⁻¹ Sym.↑) • (Sym.H Sym.↑) • (Sym.S⁻¹ Sym.↑) • Sym.CZ • (Sym.H Sym.↑) • (Sym.S⁻¹ Sym.↑) • (Sym.S⁻¹ Sym.↓) ≈⟨ sym (cong ( Sym'.lemma-cong↑ _ _ (lemma-g* p-1)) (cong refl₁ (cong (Sym'.lemma-cong↑ _ _ (lemma-g* p-1)) (cong refl₁ (cong refl₁ (cong (Sym'.lemma-cong↑ _ _ (lemma-g* p-1)) ((lemma-g* p-1)))))))) ⟩
  (g* S⁻¹ Sym.↑) • (Sym.H Sym.↑) • (g* S⁻¹ Sym.↑) • Sym.CZ • (Sym.H Sym.↑) • (g* S⁻¹ Sym.↑) • (g* S⁻¹ Sym.↓) ≈⟨ sym (cong (lemma-g*-↑ S⁻¹) (cong refl₁ (cong (lemma-g*-↑ S⁻¹) (cong refl₁ (cong refl₁ (cong (lemma-g*-↑ S⁻¹) (refl))))))) ⟩
  g* ((S⁻¹ ↑) • (H ↑) • (S⁻¹ ↑) • CZ • (H ↑) • (S⁻¹ ↑) • (S⁻¹ ↓)) ∎
  where
  open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_ ; refl to refl₁ ; cong to cong₁ ; sym to sym₁ ; axiom to axiom₁) using ()
  open PP ((₁₊ n) QRel,_===₁_)
  open SR word-setoid
g-well-defined {₁₊ n} selinger-c11 = begin
  g* (CZ • H ↓ • CZ) ≈⟨ refl₁ ⟩
  (Sym.CZ • Sym.H Sym.↓ • Sym.CZ) ≈⟨ axiom₁ _QRel,_===₁_.selinger-c11 ⟩
  (Sym.S⁻¹ Sym.↓) • (Sym.H Sym.↓) • (Sym.S⁻¹ Sym.↓) • Sym.CZ • (Sym.H Sym.↓) • (Sym.S⁻¹ Sym.↓) • (Sym.S⁻¹ Sym.↑) ≈⟨ sym (cong ( (lemma-g* p-1)) (cong refl₁ (cong ((lemma-g* p-1)) (cong refl₁ (cong refl₁ (cong ((lemma-g* p-1)) (Sym'.lemma-cong↑ _ _ (lemma-g* p-1)))))))) ⟩
  (g* S⁻¹ Sym.↓) • (Sym.H Sym.↓) • (g* S⁻¹ Sym.↓) • Sym.CZ • (Sym.H Sym.↓) • (g* S⁻¹ Sym.↓) • (g* S⁻¹ Sym.↑) ≈⟨ cong refl₁ (cong refl₁ (cong refl₁ (cong refl₁ (cong refl₁ (cong refl₁ (sym (lemma-g*-↑ S⁻¹))))))) ⟩
  g* ((S⁻¹ ↓) • (H ↓) • (S⁻¹ ↓) • CZ • (H ↓) • (S⁻¹ ↓) • (S⁻¹ ↑)) ∎
  where
  open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_ ; refl to refl₁ ; cong to cong₁ ; sym to sym₁ ; axiom to axiom₁) using ()
  open PP ((₁₊ n) QRel,_===₁_)
  open SR word-setoid
g-well-defined {n} selinger-c12 = axiom _QRel,_===₁_.selinger-c12
g-well-defined {n} selinger-c13 = axiom _QRel,_===₁_.selinger-c13
g-well-defined {n} selinger-c14 = axiom _QRel,_===₁_.selinger-c14
g-well-defined {n} selinger-c15 = axiom _QRel,_===₁_.selinger-c15
g-well-defined {(₁₊ n)} (comm-H {g = g₁}) = begin
  g* ([ g₁ Gen₂.ₛ ↑] • SymDerived.H) ≈⟨ refl₁ ⟩
  g* ([ g₁ Gen₂.ₛ ↑]) • Sym.H ≈⟨ refl₁ ⟩
  g* ([ g₁ ↑]) Sym.↑ • Sym.H ≈⟨ sym₁ (Sym'.lemma-comm-H-w↑ (g g₁)) ⟩
  Sym.H • g* ([ g₁ ↑]) Sym.↑ ≈⟨ refl₁ ⟩
  g* (SymDerived.H • [ g₁ Gen₂.ₛ ↑]) ∎
  where
  open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_ ; refl to refl₁ ; cong to cong₁ ; sym to sym₁ ; axiom to axiom₁)
  open PP ((₁₊ n) QRel,_===₁_)
  open SR word-setoid
g-well-defined {(₁₊ n)} (comm-S {g = g₁}) = begin
  g* ([ g₁ Gen₂.ₛ ↑] • SymDerived.S) ≈⟨ refl₁ ⟩
  g* ([ g₁ Gen₂.ₛ ↑]) • Sym.S ≈⟨ refl₁ ⟩
  g* ([ g₁ ↑]) Sym.↑ • Sym.S ≈⟨ sym₁ (Sym'.lemma-comm-S-w↑ (g g₁)) ⟩
  Sym.S • g* ([ g₁ ↑]) Sym.↑ ≈⟨ refl₁ ⟩
  g* (SymDerived.S • [ g₁ Gen₂.ₛ ↑]) ∎
  where
  open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_ ; refl to refl₁ ; cong to cong₁ ; sym to sym₁ ; axiom to axiom₁)
  open PP ((₁₊ n) QRel,_===₁_)
  open SR word-setoid
g-well-defined {(₁₊ n)} (comm-CZ {g = g₁}) = begin
  g* ([ g₁ Gen₂.ₛ Gen₂.ₛ ↑] • SymDerived.CZ) ≈⟨ refl₁ ⟩
  g* ([ g₁ Gen₂.ₛ Gen₂.ₛ ↑]) • Sym.CZ ≈⟨ refl₁ ⟩
  g* ([ g₁ ↑]) Sym.↑  Sym.↑ • Sym.CZ ≈⟨ sym₁ (Sym'.lemma-comm-CZ-w↑ (g g₁)) ⟩
  Sym.CZ • g* ([ g₁ ↑]) Sym.↑ Sym.↑ ≈⟨ refl₁ ⟩
  g* (SymDerived.CZ • [ g₁ Gen₂.ₛ Gen₂.ₛ ↑]) ∎
  where
  open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_ ; refl to refl₁ ; cong to cong₁ ; sym to sym₁ ; axiom to axiom₁)
  open PP ((₁₊ n) QRel,_===₁_)
  open SR word-setoid
g-well-defined {₁₊ n} (derived-CZ k) = begin
  g* [ CZ-gen k ↑] ≈⟨ refl₁ ⟩
  Sym.CZ ^ toℕ k ≈⟨ sym (lemma-g*-CZ (toℕ k) ) ⟩
  g* (CZ ^ toℕ k) ∎
  where
  open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_ ; refl to refl₁ ; cong to cong₁ ; sym to sym₁ ; axiom to axiom₁)
  open PP ((₁₊ n) QRel,_===₁_)
  open SR word-setoid

g-well-defined {(₁₊ n)} (cong↑ {w = w} {v = v} x) = begin
  g* (w SymDerived.↑) ≈⟨ lemma-g*-↑ w ⟩
  g* w Sym.↑ ≈⟨ Sym'.lemma-cong↑ _ _ (g-well-defined x) ⟩
  g* v Sym.↑ ≈⟨ sym (lemma-g*-↑ v) ⟩
  g* (v SymDerived.↑) ∎
  where
  open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_ ; refl to refl₁ ; cong to cong₁ ; sym to sym₁ ; axiom to axiom₁)
  open PP ((₁₊ n) QRel,_===₁_)
  open SR word-setoid


f-left-inv-gen : let open PB ((n) QRel,_===₂_) renaming (_≈_ to _≈₂_) in
  ∀ x -> [ x ↑] ≈₂ (f'*) (g x)
f-left-inv-gen {₁₊ n} (SymDerived.H-gen k) = begin
  [ H-gen k ↑] ≈⟨ axiom (derived-H k) ⟩
  H ^ toℕ k ≈⟨ sym (lemma-f'*-H (toℕ k)) ⟩
  f'* (Sym.H ^ toℕ k) ∎
  where
  open PB ((₁₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) using ()
  open PP ((₁₊ n) QRel,_===₂_)
  open SR word-setoid
f-left-inv-gen {₁₊ n} (SymDerived.S-gen k) = begin
  [ S-gen k ↑] ≈⟨ axiom (derived-S k) ⟩
  S ^ toℕ k ≈⟨ sym (lemma-f'* (toℕ k)) ⟩
  f'* (Sym.S ^ toℕ k) ∎
  where
  open PB ((₁₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) using ()
  open PP ((₁₊ n) QRel,_===₂_)
  open SR word-setoid

f-left-inv-gen {₁₊ n} (SymDerived.CZ-gen k) = begin
  [ CZ-gen k ↑] ≈⟨ axiom (derived-CZ k) ⟩
  CZ ^ toℕ k ≈⟨ sym (lemma-f'*-CZ (toℕ k)) ⟩
  f'* (Sym.CZ ^ toℕ k) ∎
  where
  open PB ((₁₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) using ()
  open PP ((₁₊ n) QRel,_===₂_)
  open SR word-setoid
f-left-inv-gen {(₁₊ n)} (x SymDerived.ₛ) = begin
  [ x Gen₂.ₛ ↑] ≈⟨ lemma-cong↑ _ _ (f-left-inv-gen x) ⟩
  f'* (g x) ↑ ≈⟨ sym (lemma-f'*-↑ (g x)) ⟩
  f'* ((g x) Sym.↑) ∎
  where
  open PB ((₁₊ n) QRel,_===₂_) renaming (_≈_ to _≈₂_) using ()
  open PP ((₁₊ n) QRel,_===₂_)
  open SR word-setoid


g-left-inv-gen : let open PB ((n) QRel,_===₁_) renaming (_≈_ to _≈₁_) in
  ∀ x -> [ x ↑] ≈₁ (g*) (f' x)
g-left-inv-gen Sym.S-gen = refl
g-left-inv-gen Sym.H-gen = refl
g-left-inv-gen Sym.CZ-gen = refl
g-left-inv-gen {(₁₊ n)} (x Sym.ₛ) = begin
  [ x Gen₁.ₛ ↑] ≈⟨ Sym'.lemma-cong↑ _ _ (g-left-inv-gen x) ⟩
  g* (f'* [ x ↑]) Sym.↑ ≈⟨ _≈₁_.refl ⟩
  g* (f'* [ x ↑] ↑) ≈⟨ _≈₁_.refl ⟩
  g* (f' (x Gen₁.ₛ)) ∎
  where
  open PB ((₁₊ n) QRel,_===₁_) renaming (_≈_ to _≈₁_) using ()
  open PP ((₁₊ n) QRel,_===₁_)
  open SR word-setoid


open import Algebra.Bundles using (Group)
open import Algebra.Morphism.Structures using (module GroupMorphisms)

open GroupMorphisms

module G1 {n} = Group-Lemmas (Gen₁ (n)) ((n) QRel,_===₁_) (grouplike₁ {n})
module G2 {n} = Group-Lemmas (Gen₂ (n)) ((n) QRel,_===₂_) (grouplike₂ {n})

Theorem-Sym-iso-SymDerived : IsGroupIsomorphism (Group.rawGroup (G1.•-ε-group {n})) (Group.rawGroup (G2.•-ε-group {n})) (f'* {n})
Theorem-Sym-iso-SymDerived {n} = StarGroupIsomorphism.isGroupIsomorphism f' g f-well-defined  f-left-inv-gen g-well-defined  g-left-inv-gen
  where
  open import Presentation.Morphism ((n) QRel,_===₁_) ((n) QRel,_===₂_)
  open GroupMorphs (grouplike₁ {n}) (grouplike₂ {n})
